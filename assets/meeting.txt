####################################################
### Concept: meeting
####################################################
meeting is temporal.
meeting is spatial.

On "SUBJECT is not meeting anybody":
	delete SUBJECT meeting list;
	then, reply "ok, SUBJECT is not meeting anybody".
On "I am not meeting anybody", _user is not meeting anybody.
On "I am not meeting anyone",  _user is not meeting anybody.

On "SUBJECT is meeting PHRASE-WHOM":
	set output format to ",LOCATOR LOCATION,WHEN";
	assert SUBJECT is meeting WHOM;
	reply "I know";
	if not, perform "list add SUBJECT meeting WHOM";
	then, reply "ok, SUBJECT is meeting ...".
On "I am meeting PHRASE-WHOM", _user is meeting WHOM.
On "help", reply "to say who you are meeting".

On "when is SUBJECT meeting PHRASE-WHOM":
	set output format to ",WHEN";
	get SUBJECT meeting list;
	if not, reply "SUBJECT is not meeting anybody";
	then, reply "SUBJECT is meeting ...".
On "when am i meeting PHRASE-WHOM", when is _user meeting WHOM.
On "what time PHRASE-X", when X.

On "where is SUBJECT meeting PHRASE-WHOM":
	set output format to ",LOCATOR LOCATION";
	get SUBJECT meeting list;
	if not, reply "SUBJECT is not meeting anybody";
	then, reply "SUBJECT is meeting ...".
On "where am i meeting PHRASE-WHOM", where is _user meeting WHOM.
On "help", reply "to find out where you are meeting the person".

On "SUBJECT is not meeting PHRASE-WHOM":
	perform "list remove SUBJECT meeting WHOM";
	if not, "I know";
	then, reply "ok, SUBJECT is not meeting WHOM".
On "I am not meeting PHRASE-WHOM", _user is not meeting WHOM.

On "is SUBJECT meeting PHRASE-WHOM":
	set output format to ",WHEN,LOCATOR LOCATION";
	perform "list exists SUBJECT meeting WHOM";
	if not, reply "no, SUBJECT is not meeting WHOM";
	then, reply "yes, SUBJECT is meeting WHOM".
On "am i meeting PHRASE-WHOM", is _user meeting WHOM.

On "who is SUBJECT meeting":
	set output format to ",at WHERE,WHEN";
	perform "list get SUBJECT meeting -";
	if not, reply "SUBJECT is not meeting anybody";
	then, reply "SUBJECT is meeting ...".
On "who am i meeting", who is _user meeting.
On "help", reply "to list who you are meeting".
