On "calculate NUMERIC-X", evaluate X.

## All the following examples maintain a running total...
On "what is NUMERIC-X":
	evaluate X;
	set subtotal to ...;
	reply "X is ...".

On "NUMERIC-X", set subtotal to X.
    
On "+ NUMERIC-X":
	evaluate SUBTOTAL plus X;
	set subtotal to ...;
	reply "plus X makes SUBTOTAL".

On "add NUMERIC-X", + X.
     
On "minus NUMERIC-X":
	evaluate SUBTOTAL minus X;
	set subtotal to ...";
	reply "minus X makes SUBTOTAL".
    
On "take away NUMERIC-X", minus X.
    
On "times NUMERIC-X":
	evaluate SUBTOTAL times X;
	set subtotal to ...;
	reply "times X makes SUBTOTAL".
    
On "times by      NUMERIC-X":
	times X;
	reply "times by X makes SUBTOTAL".

On "multiplied by NUMERIC-X":
	times X;
	reply "multiplied by X makes SUBTOTAL".

On "equals", reply "SUBTOTAL".

On "divided by NUMERIC-X":
	evaluate SUBTOTAL divided by X;
	set subtotal to ...";
	reply "divided by X makes SUBTOTAL".


# added for fizzbuzz.txt
#
On "is NUMERIC-N divisible by NUMERIC-M":
	what is N divided by M;
	is remainder set to 0;
	if so, reply "yes";
	if not, reply "no".
	
On "is NUMERIC-N divisible by AND-LIST-M":
	is N divisible by FIRST-OF-M;
	if not, reply "no";
	is N divisible by REST-OF-M;
	if not, reply "no, N is not divisible by M";
	reply "yes, N is divisible by M".

On "what is the remainder":
	get the value of remainder;
	if not, reply "sorry, i don't know";
	reply "the remainder is ...".
	
On "NUMERIC-X is equal to NUMERIC-Y":
	evaluate X - Y;
	set temp to ... ;
	is temp set to 0;
	if so, reply "yes, it is";
	reply "no, it isn't".
