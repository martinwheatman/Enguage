####################################################
### Concept: X needs Y
####################################################

This concept is spatial.

###############################################################################
# Multiple singular additions
On "SUBJECT needs AND-LIST-THESE": #a cup of coffee and some biscuits
	SUBJECT needs FIRST-OF-THESE;
	SUBJECT needs REST-OF-THESE; 
	if not, reply "i do not understand";
	reply "ok, SUBJECT needs THESE".

On "i need AND-LIST-THESE": #a cup of coffee and some biscuits
	i need FIRST-OF-THESE;
	i need REST-OF-THESE; 
	if not, reply "i do not understand";
	reply "ok, you need THESE".

###############################################################################
# GROUP additions - singular group
# martin and ruth need a coffee and a tea
On "AND-LIST-SUBJECTS need PHRASE-THESE":
	GROUPED-SUBJECTS needs THESE;
	reply "ok, SUBJECTS need THESE".

On "what do AND-LIST-SUBJECTS need":
	get GROUPED-SUBJECTS needs list;
	if not, reply "SUBJECTS do not need anything";
	reply "SUBJECTS need ...".

On "AND-LIST-SUBJECTS do not need PHRASE-THIS":
	THIS exists in GROUPED-SUBJECTS needs list;
	if not, reply "yes, i know";
	remove THIS from GROUPED-SUBJECTS needs list;
	reply "ok, SUBJECTS do not need ...".

On "AND-LIST-SUBJECTS do not need anything":
	remove everything from GROUPED-SUBJECTS needs list;
	reply "ok, SUBJECTS do not need anything [LOCATOR LOCATION]".

###############################################################################
# Combinations - multiple single subjects, phrased objects
# martin and james and ruth all need a coffee and a tea.
On "SUBJECT all need PHRASE-THESE", SUBJECT needs THESE.
On "AND-LIST-SUBJECTS all need PHRASE-THESE":
	FIRST-OF-SUBJECTS needs THESE;
	REST-OF-SUBJECTS all need THESE;
	if not, reply "i do not understand";
	reply "ok, SUBJECTS all need THESE".

# martin and ruth both need a coffee and a tea.
On "AND-LIST-SUBJECTS both need PHRASE-THESE":
	SUBJECTS all need THESE;
	reply "ok, SUBJECTS both need THESE".

###############################################################################
# Various singular additions
On "i need PHRASE-THIS":
	set subject to _user;
	THIS exists in SUBJECT needs list;
	if so, reply "yes, i know";
	if not, add THIS to SUBJECT needs list;
	if not, append THIS onto SUBJECT needs list;
	reply "ok, you need ... ".

On "i need NUMERIC-QUANTITY PHRASE-THIS":
	set subject to _user;
	QUANTITY THIS exists in SUBJECT needs list;
	if so, reply "yes, i know";
	if not, add QUANTITY THIS to SUBJECT needs list;
	reply "ok, you need ... ".

On "SUBJECT needs PHRASE-THESE":
	THESE exists in SUBJECT needs list;
	if so, reply "yes, i know";
	if not, add THESE to SUBJECT needs list;
	if not, append THESE onto SUBJECT needs list;
	reply "ok, SUBJECT needs ...".

On "SUBJECT needs NUMERIC-QUANTITY PHRASE-THIS":
	QUANTITY THIS exists in SUBJECT needs list;
	if so, reply "yes, i know";
	if not, add QUANTITY THIS to SUBJECT needs list;
	reply "ok, SUBJECT needs ... ".

On "SUBJECT needs NUMERIC-QUANTITY UNIT of PHRASE-THIS":
	QUANTITY UNIT of THIS exists in SUBJECT needs list;
	if so, reply "yes, i know";
	if not, add QUANTITY UNIT of THIS to SUBJECT needs list;
	reply "ok, SUBJECT needs ...".

## Elaboration of subsequent utterances
On "and another":
	is subject set to _user;
	if so, i need another THIS;
	if not, SUBJECT needs another THIS.

On "and another THIS":
	is subject set to _user;
	if so, i need another THIS;
	if not, SUBJECT needs another THIS.

On "and another UNITS of PHRASE-THIS":
	is subject set to _user;
	if so,  i need another UNITS of THIS;
	if not, SUBJECT needs another UNITS of THIS.


####################################################
### Pronoun ancillaries - it is/they are from X ###
### LOCATOR LOCATION are implied anyway! A bug? ###
####################################################

On "the PHRASE-THIS is": 
	perform "items update SUBJECT needs THIS";
	reply "ok, THIS is LOCATOR LOCATION".

On "it is":
	the THIS is;
	reply "ok, it is LOCATOR LOCATION".

On "the AND-LIST-THESE are":
	the FIRST-OF-THESE is;
	the REST-OF-THESE are;	
	reply "ok, THESE are LOCATOR LOCATION".
	
On "they are":
	the THESE are;
	reply "ok, they are LOCATOR LOCATION".
	
#########################################
### and bananas and apples
#########################################

On "and AND-LIST-THINGS", we need THINGS.
	
#########################################
### does not need... removals
#########################################

On "SUBJECT does not need AND-LIST-THESE":
	SUBJECT does not need FIRST-OF-THESE;
	SUBJECT does not need  REST-OF-THESE;
	reply "ok, SUBJECT don't need THESE".

On "i do not need AND-LIST-THESE":
	set subject to _user;
	_user does not need THESE;
	reply "ok, you don't need THESE".

On "i do not need anything":
	set subject to _user;
	remove everything from SUBJECT needs list;
	reply "ok, you do not need anything [LOCATOR LOCATION]".

On "SUBJECT does not need anything":
	#answering yes or no ask i am about to delete the needs list is this ok;
	#if not, reply "ok, let us leave things as they are";
	remove everything from SUBJECT needs list;
	reply "ok, SUBJECT does not need anything [LOCATOR LOCATION]".

On "i do not need PHRASE-THIS":
	set subject to _user;
	THIS exists in _user needs list;
	if not, reply "yes, i know";
	remove THIS from _user needs list;
	if not, reply "ok, _user does not need THIS";
	reply "ok, you do not need ...".
	
On "SUBJECT does not need PHRASE-THESE":
	THESE exists in SUBJECT needs list;
	if not, reply "yes, i know";
	remove THESE from SUBJECT needs list;
	if not, reply "ok, SUBJECT does not need THESE";
	reply "ok, SUBJECT does not need ...".

On "SUBJECT does not need NUMERIC-QUANTITY PHRASE-THIS":
	THIS exists in SUBJECT needs list;
	if not, reply "yes, i know";
	remove QUANTITY THIS from SUBJECT needs list;
	if not, reply "i'm sorry, SUBJECT does not need QUANTITY THIS";
	reply "ok, SUBJECT does not need ...".

On "SUBJECT does not need NUMERIC-QUANTITY UNIT of PHRASE-THIS":
	THIS exists in SUBJECT needs list;
	if not, reply "yes, i know";
	remove QUANTITY UNIT of THIS from SUBJECT needs list;
	if not, reply "i'm sorry, SUBJECT does not need QUANTITY UNIT of THIS";
	reply "ok, SUBJECT does not need ...".

On "i do not need any PHRASE-THIS":
	set subject to _user;
	THIS exists in _user needs list;
	if not, reply "yes, i know";
	remove any THIS from _user needs list;
	if not, reply "i'm sorry, you do not need any THIS";
	reply "ok, you do not need any THIS".

On "SUBJECT does not need any PHRASE-THIS":
	THIS exists in SUBJECT needs list;
	if not, reply "yes, i know";
	remove any THIS from SUBJECT needs list;
	if not, reply "i'm sorry, SUBJECT does not need any THIS";
	reply "ok, SUBJECT does not need any THIS".


#########################################
### do/does i/X need ...
#########################################
On "what does SUBJECT need":
	get SUBJECT needs list;
	if not, reply "SUBJECT does not need anything";
	reply "SUBJECT needs ...".

On "what  do   i   need":
	set subject to _user;
	get _user needs list;
	if not, reply "you do not need anything";
	reply "you need ...".

On "do i need PHRASE-THIS":
	perform "items exists _user needs THIS";
	if not, reply "no, you does not need THIS";
	reply "yes, you need THIS".

On "do i need NUMERIC-QUANTITY PHRASE-THIS":
	perform "items exists _user needs THIS QUANTITY";
	if not, reply "no, you do not need QUANTITY THIS";
	reply "yes, you need QUANTITY THIS".

On "do i need NUMERIC-QUANTITY UNIT of PHRASE-THIS":
	perform "items exists _user needs THIS QUANTITY UNIT";
	if not, reply "no, you does not need QUANTITY UNIT of THIS";
	reply "yes, you need QUANTITY UNIT of THIS".

On "does SUBJECT need PHRASE-THIS":
	perform "items exists SUBJECT needs THIS";
	if not, reply "no, SUBJECT does not need THIS";
	reply "yes, SUBJECT needs THIS".

On "does SUBJECT need NUMERIC-QUANTITY PHRASE-THIS":
	perform "items exists SUBJECT needs THIS QUANTITY";
	if not, reply "no, SUBJECT does not need QUANTITY THIS";
	reply "yes, SUBJECT needs QUANTITY THIS".

On "does SUBJECT need NUMERIC-QUANTITY UNIT of PHRASE-THIS":
	perform "items exists SUBJECT needs THIS QUANTITY UNIT";
	if not, reply "no, SUBJECT does not need QUANTITY UNIT of THIS";
	reply "yes, SUBJECT needs QUANTITY UNIT of THIS".


#########################################
# how many...
#########################################
On "how many PHRASE-THIS do i need":
	perform "items exists SUBJECT needs THIS";
	if not, reply "you do not need THIS";
	how many THIS in SUBJECT needs list;
	reply "..., you need ... THIS".

On "how many UNIT of PHRASE-THIS does SUBJECT need":
	perform "items exists _user needs THIS";
	if not, reply "SUBJECT does not need THIS";
	how many UNIT of THIS in _user needs list;
	reply "..., SUBJECT needs ... UNIT of THIS".

On "how many PHRASE-THIS does SUBJECT need":
	perform "items exists SUBJECT needs THIS";
	if not, reply "SUBJECT does not need THIS";
	how many THIS in SUBJECT needs list;
	reply "..., SUBJECT needs ... THIS".


#########################################
# Useful first person utterances
#########################################
On "i        need         NUMERIC-QUANTITY", i need QUANTITY THIS.
On "i        need         nothing",      _user does not need anything.

On "i        need to get  PHRASE-THIS", _user needs THIS.
On "i do not need to get  PHRASE-THIS", _user does not need THIS.

On "do i need PHRASE-THIS",          does _user need THIS.


########################
# we => i, a fix for now
########################
On "we need PHRASE-THIS",         i need THIS.
On "what do we need",             what do i need.
On "do we need PHRASE-THESE",     do i need THESE.
On "we do not need PHRASE-THESE", i do not need THESE.
On "we do not need anything",     i do not need anything.
On "we        need nothing",      i need nothing.
