# is: class model
On "X is an entity":
	assert X is a class;
	if so, reply "no, X is a class";
	create entity X;
	reply "ok, X is an entity".

On "is X an entity":
	assert X is a class;
	if so, reply "no, X is a class";
	entity X exists;
	if not, reply "no, X does not exist";
	reply "yes, X is an entity".

On "X is a class":
	assert that a instanceof link from X exists;
	if so, reply "no, X is an entity";
	create an isa link from X to class;
	reply "ok, X is a class".
	
On "a DERIVED is a BASE":
	DERIVED is a class;
	BASE is a class;
	create an isa link from DERIVED to BASE;
	reply "ok, a DERIVED is a BASE".

On "X is a Y":
	X is an entity;
	if not, reply "sorry, X is a class";
	Y is a class;
	create an instanceof link from X to Y;
	reply "ok, X is a Y".

On "is THIS a CLASS":
	an isa link from CLASS exists;
	if not, reply "sorry, CLASS is not a class";
	entity THIS exists;
	if not, reply "sorry, THIS does not exist";
	perform "link exists THIS instanceof CLASS";
	if so, reply "yes, THIS is an instance of a CLASS";
	perform "link get THIS instanceof";
	perform "link transExists ... isa CLASS";
	if not, reply "no, THIS is not a CLASS";
	reply "yes, THIS is a CLASS".
	
On "is CLASS a class":
	entity CLASS exists;
	if not, reply "sorry, CLASS does not exist";
	perform "link exists CLASS isa";
	if not, reply "no, CLASS is not a class";
	reply "yes, CLASS is a class".
