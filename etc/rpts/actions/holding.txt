## Natural information:
## The default value is unknown.
## The name 'nobody' is used to indicate known nobody
## Otherwise a name indicates whose hand someone is holding.

On "X is holding hands with Y":
	create a holdinghands link from X to Y;
	reply "ok, X is holding hands with Y".

On "i am holding hands with Y":
	martin is holding hands with Y;
	reply "ok, you are holding hands with Y".

On "X is not holding hands with Y":
	# delete a holdinghands link from X to Y;
	perform "link delete X holdinghands Y";
	if not, reply "yes, i know";
	reply "ok, X is not holding hands with Y".

On "who's hand am i holding", whose hand am I holding.
On "whose hand am i holding":
	perform "link exists martin holdinghands";
	if not, reply "sorry, i do not know";
	perform "link get martin holdinghands";
	set someone to ...;
	is someone set to nobody;
	if so, reply "sorry, you are not holding anyone's hand";
	is someone set to i;
	if so, reply "ok, you are holding my hand";
	reply "ok, you are holding SOMEONE''s hand".
	
On "we are holding hands":
	who are we;
	set someone to ...;
	i am holding hands with SOMEONE;
	is someone set to i;
	if so, reply "ok, we are holding hands";
	reply "ok, you are holding hands with SOMEONE".

	
On "i am not holding anyone's hand":
	i am holding hands with nobody;
	reply "ok, you are not holding anyone's hand".

